/**
 *
 * File:
 *   slave.ycp
 *
 * Module:
 *   Network/YPServer
 *
 * Summary:
 *   YPServer module.
 *
 * Authors:
 *   Dan Vesely <dan@suse.cz>
 *   Martin Vidner <mvidner@suse.cz>
 *
 * $Id$
 *
 * YPServer module part.
 *
 */


{

    textdomain "nis_server";

    import "Wizard";
    import "Popup";

    import "CWMFirewallInterfaces";
    import "NisServer";
    import "Nis";
    import "Address";
    include "nis_server/routines.ycp";

    /**
     * Slave dialog
     * @return `back, `abort or `next
     */
    define symbol SlaveDialog () ``{

	map<string,any> firewall_widget = GetFirewallWidget ();
	term firewall_layout = firewall_widget["custom_widget"]:`VBox ();

	// help text 1/2
	string helptext = _("<p>Enter the NIS <b>domain</b> and the IP <b>address</b> or host name of the master NIS server.</p>");
	// help text 2/2
	helptext = helptext + _("<p>If this host is also a NIS client using this machine as a server, check the corresponding option.</p>")

	+ firewall_widget["help"]:"";

	term contents = `HVSquash (`VBox (
	    // textentry label
	    `InputField (`id (`domain), `opt(`hstretch), _("N&IS domain name:"),
		NisServer::domain),
	    `VSpacing (0.5),
	    `InputField (`id (`master_ip), `opt (`hstretch),
		// text entry label
		("NIS &master server:"), NisServer::ui_master_ip),
	    `VSpacing (),
	    `Left (`CheckBox (`id (`also_client),
		// checkbox label
		_("This host is also a NIS &client"), NisServer::start_ypbind)
	    ),
	    `VSpacing (2),
	    firewall_layout
	));

	// To translators: dialog label
	Wizard::SetContents (_("Slave Server Setup"),
	    contents, helptext, true, true);

	CWMFirewallInterfaces::OpenFirewallInit (firewall_widget, "");

	map event = $[];
	any ui	= nil;

	repeat
	{
	    event	= UI::WaitForEvent ();
	    ui		= event["ID"]:nil;
	    CWMFirewallInterfaces::OpenFirewallHandle(firewall_widget,"",event);
	    if (ui == `cancel)
	    {
		ui = `abort;
	    }

	    if (ui == `next)
	    {
		string master_ip  =
		    (string) UI::QueryWidget (`id (`master_ip), `Value);
		string domainname =
		    (string) UI::QueryWidget (`id (`domain), `Value);
		boolean start_ypbind	=
		    (boolean) UI::QueryWidget (`id (`also_client), `Value);
		if (!Address::Check4(master_ip))
		{
		    // To translators: error message
		    UI::SetFocus (`id (`master_ip));
		    Popup::Error (Address::Valid4());
		    ui = `again;
		    continue;
		}
		else if (!Nis::check_nisdomainname (domainname))
		{
		    UI::SetFocus (`id (`domain));
		    Popup::Error (Nis::valid_nisdomainname ());
		    ui = `again;
		    continue;
		}
		if (master_ip != NisServer::ui_master_ip ||
		    domainname!= NisServer::domain ||
		    start_ypbind != NisServer::start_ypbind)
		{
		    NisServer::modified	= true;
		}

		CheckForDHCPClient (domainname);
		CWMFirewallInterfaces::OpenFirewallStore (
		    firewall_widget,"",event);
		NisServer::start_ypbind = start_ypbind;
		NisServer::ui_master_ip	= master_ip;
		NisServer::domain	= domainname;
	    }

	    if (ui == `abort && !Popup::ReallyAbort (NisServer::modified))
		ui = `again;
	}
	until (ui == `next || ui == `back || ui == `abort);

	return (symbol) ui;
    }
}
