/**
 * File:	include/nis_server/dialogs.ycp
 * Package:	Configuration of nisServer
 * Summary:	Dialogs definitions
 * Authors:	Martin Vidner <mvidner@suse.cz>
 *              Dan Vesely <dan@suse.cz>
 *
 * $Id$
 */

{
    textdomain "nis_server";

    import "NisServer";
    include "nis_server/what.ycp";
    include "nis_server/slave.ycp";
    include "nis_server/master.ycp";
    include "nis_server/details.ycp";
    include "nis_server/slaves.ycp";
    include "nis_server/maps.ycp";
    include "nis_server/securenets.ycp";

    import "Require";
    import "Wizard";
    import "Label";
    import "Popup";

    /**
     * Read settings dialog
     * @return `abort if aborted and `next otherwise
     */
    global define symbol ReadDialog() ``{
	boolean ret = NisServer::Read();
	return ret ? `next : `abort;
    }

    /**
     * Write settings dialog
     * @return `abort if aborted and `next otherwise
     */
    global define symbol WriteDialog() ``{
	Wizard::RestoreNextButton();
	UI::ChangeWidget (`id (`next), `Enabled, false);
	UI::ChangeWidget (`id (`abort), `Enabled, false);

	boolean ret = NisServer::Write();
	return ret ? `next : `abort;
    }

    /**
     * Popup to confirm after finish is pressed
     * @return `yes or `back
     */
    global define symbol FinishPopup () ``{
	if (Popup::ContinueCancelHeadline (
		// To translators: ContinueCancel Popup headline
		_("Finish"),
		// To translators: ContinueCancel Popup
		_("Really save configuration ?")))
	{
	    return `yes;
	}
	return `back;
    }

    /**
     * Popup to confirm vhen exitting without saving
     * @return `exit or `back
     */
    global define symbol ExitPopup () ``{
	if (Popup::YesNoHeadline (
		// To translators: YesNo Popup headline
		_("Exit"),
		// To translators: YesNo Popup
		_("Really exit configuration without saving ?")))
	{
	    return `exit;
	}
	return `back;
    }

    /**
     * Popup with details error
     * @return `ok
     */
    global define symbol DetailsPopup () ``{

				// To translators: popup label
	Popup::LongText (_("Error details"), `RichText (NisServer::error_details), 50, 20 );
	return `ok;
    }

    /**
     * Popup displaying configuration result
     * @return `ok or `details
     */
    global define symbol ResultPopup () ``{

	string message = "";
	if (size (NisServer::error_msg) > 0)
	{
				// To translators: popup label
	    message = sformat (_("Error during configuration:\n%1"),
			       NisServer::error_msg);

				// To translators: Error popup
	    return Popup::AnyQuestion (Label::ErrorMsg(), message, Label::OKButton(), _("&Details"), `focus_yes) ? `ok : `details;
	    //Popup::Error (message);
	}
	else
	{
	    if (NisServer::start_ypbind &&
		NisServer::old_domainname != "" &&
		NisServer::old_domainname != NisServer::domain)
	    {
		// To translators: final popup
		Popup::Warning (_("The domain has changed.
You must reboot for the changes to take effect."));

	    }
	    else
	    {
		// To translators: final popup
		message = _("The configuration was succesful!");
		Popup::Notify (message);
	    }
	}

	return `ok;
    }

    /**
     * Checks if the YP server package is installed
     * and calls installation if not
     * @return `master or `slave
     */
    global define symbol InstallServer () ``{
	if (!Require::AreAllPackagesInstalled (["ypserv"]))
	{
	    Require::DoInstallAndRemove (["ypserv"], []);
	}

	return NisServer::ui_what;
    }
}
