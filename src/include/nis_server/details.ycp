/* ------------------------------------------------------------------------------
 * Copyright (c) 2006-2012 Novell, Inc. All Rights Reserved.
 *
 *
 * This program is free software; you can redistribute it and/or modify it under
 * the terms of version 2 of the GNU General Public License as published by the
 * Free Software Foundation.
 *
 * This program is distributed in the hope that it will be useful, but WITHOUT
 * ANY WARRANTY; without even the implied warranty of MERCHANTABILITY or FITNESS
 * FOR A PARTICULAR PURPOSE.  See the GNU General Public License for more details.
 *
 * You should have received a copy of the GNU General Public License along with
 * this program; if not, contact Novell, Inc.
 *
 * To contact Novell about this file by physical or electronic mail, you may find
 * current contact information at www.novell.com.
 * ------------------------------------------------------------------------------
 */

/**
 *
 * File:
 *   details.ycp
 *
 * Module:
 *   Network/YPServer
 *
 * Summary:
 *   YPServer module.
 *
 * Authors:
 *   Dan Vesely <dan@suse.cz>
 *
 * $Id$
 *
 * YPServer module part.
 *
 */


{

    textdomain "nis_server";

    import "Label";
    import "Message";
    import "Mode";
    import "NisServer";
    import "Popup";
    import "Wizard";

    include "nis_server/routines.ycp";

    /**
     * Details dialog
     * @return `back, `abort or `next
     */
    define symbol DetailsDialog () ``{

	string srcdir	= NisServer::pwd_srcdir;

	integer minuid	= NisServer::minuid;
	integer mingid	= NisServer::mingid;
	boolean merge_passwd	= NisServer::merge_passwd;

	// help text 1/3
	string helptext = _("<p>You can change NIS server source directory (usually
<i>'/etc'</i>).</p>");

	// help text 2/3
	helptext = helptext + _("<p>Select if your <i>passwd</i> file should be merged with the <i>shadow</i>
file (only possible if the <i>shadow</i> file exists).</p>
");

	// help text 3/3
	helptext = helptext + _("<p>You can also adjust the minimum user and group id.</p>");

	term minimals = `HBox (
	    // To translators: intfield label
	    `IntField (`id (`minuid), _("Minimum &UID"), 0, 50000, minuid),
	    `HSpacing (),
	    // To translators: intfield label
	    `IntField (`id (`mingid), _("Minimum &GID"), 0, 50000, mingid)
	);

	term contents = `HVSquash (`VBox (
	    `InputField (`id (`srcdir), `opt(`notify, `hstretch),
		// To translators: textentry label
		_("&YP Source directory"), srcdir),
	    `VSpacing (0.5),
	    // check box label
	    `Left (`CheckBox (`id (`merge_passwd), _("Merge pa&sswords"),
		merge_passwd)
	    ),
	    `VSpacing (0.5),
	    minimals
	));

	// To translators: dialog label
	Wizard::SetContents (_("NIS Master Server Details Setup"),
	    contents, helptext, true, true);

	Wizard::SetBackButton(`back, Label::CancelButton() );
	Wizard::SetNextButton(`next, Label::OKButton() );
	Wizard::HideAbortButton();

	// If the source directory does not exist, it will be created
	// with empty passwd, group, shadow
	// If it already exists, check whether shadow exist
	// and disable some options accordingly.
	// (#15624)
	boolean srcdir_exists = nil;
	boolean shadow_exists = nil;
	boolean change_enabled = true;

	symbol ui = `again;

	repeat
	{
	    if (change_enabled)
	    {
		// srcdir now has an up-to-date value
		srcdir_exists = SCR::Read (.target.dir, srcdir) != nil;
		shadow_exists  = !srcdir_exists || SCR::Read (.target.size, sformat ("%1/shadow", srcdir)) != -1;
		UI::ChangeWidget (`id (`merge_passwd), `Enabled, shadow_exists);
	    }

	    ui = (symbol) UI::UserInput ();
	    if (ui == `cancel)
	    {
		ui = `abort;
	    }

	    change_enabled = (ui == `srcdir);
	    srcdir = (string) UI::QueryWidget (`id (`srcdir), `Value);
	    if (ui == `abort && !Popup::ReallyAbort (NisServer::modified))
		ui = `again;
	    if (ui == `next)
	    {
		if (SCR::Read (.target.dir, srcdir) == nil && !Mode::config ())
		{
		    UI::SetFocus (`id (`srcdir));
		    ui = Popup::YesNo (
			Message::DirectoryDoesNotExistCreate (srcdir))
			? `next : `again;
		}
		merge_passwd	= (boolean) UI::QueryWidget (`id (`merge_passwd), `Value);
		minuid		= (integer) UI::QueryWidget (`id (`minuid), `Value);
		mingid		= (integer) UI::QueryWidget (`id (`mingid), `Value);
		if (NisServer::minuid != minuid ||
		    NisServer::mingid != mingid ||
		    NisServer::merge_passwd != merge_passwd ||
		    NisServer::pwd_srcdir != srcdir)
		{
		    NisServer::modified	= true;
		    NisServer::minuid	= minuid;
		    NisServer::mingid	= mingid;
		    NisServer::merge_passwd	= merge_passwd;
		    NisServer::pwd_srcdir	= srcdir;
		}
	    }
	}
	until (contains ([`back, `next, `abort], ui));

	Wizard::RestoreBackButton();
	Wizard::RestoreNextButton();
	Wizard::RestoreAbortButton();
	return ui;
    }
}
